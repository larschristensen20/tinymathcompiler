grammar dk.sdu.mdsd.Mathinterpreter with org.eclipse.xtext.common.Terminals

generate mathinterpreter "http://www.sdu.dk/mdsd/Mathinterpreter"

Model:
	declarations+=Declaration*;

Declaration:
	 ExternalDef | MathExp;

MathExp:
	'result' id=STRING 'is' exp=Exp;

Exp:
	PlusOrMinus;

ExternalDef:
	'external' name=ID '(' parameters+=Parameter (',' parameters+=Parameter)* ')';

Parameter:
	type=ID varName=ID;

ExternalUse:
	{External} external=[ExternalDef] '(' arguments+=INT (',' arguments+=INT)* ')';
	
Atomic returns Exp:
	{Number} value=INT;

Parenthesis returns Exp:
	{Parenthesis} '(' exp=Exp ')';

Primary returns Exp:
	ExternalUse | Atomic | Parenthesis;

PlusOrMinus returns Exp:
	MultOrDiv (({Plus.left=current} op='+' | {Minus.left=current} op='-') 
			right=MultOrDiv)*;

MultOrDiv returns Exp:
	Primary (({Mult.left=current} op='*' | {Div.left=current} op='/') 
			right=Primary)*;